env int doorSensor_id = 1;
env int windowSensor_id = 2;

env int user_key = 3;
env int user_code = 4;
env int allowed_attempts = 5;

env int user_time_before_code = 2;
env int user_time_entering_code = 3;

reactiveclass Controller(10) {
    knownrebecs { 
        Sensor window_sensor, door_sensor; 
        MotionSensor motion;
        Authentication authentication;
        Door door;
    }

    statevars {
        boolean armed;
        boolean door_open;
        boolean window_open;
        boolean alarm; 
        boolean motion_triggered; 
        boolean door_locked;
    }
    
    Controller() {
        armed = false;
        door_open = false;
        window_open = false;
        alarm = false; 
        motion_triggered = false; 
        door_locked = false;
    }

    msgsrv authenticatedUser() {
        if (!armed) {
            if (door_open || window_open){
                //notify error, you work now, faster
            }
            
            self.setArmed();
        
        } else {
            self.setUnarmed();
        }
    }
    
    msgsrv setArmed() {
    	armed = true;
        door.setDoorlocked();
    }
    
    
    msgsrv setUnarmed() {
    	armed = false;
        door.setDoorUnlocked();
    }

    msgsrv setSensorState(int sensor, boolean state){
        if (sensor == windowSensor_id) {
            window_open = state;
        }
        
        if (sensor == doorSensor_id) {
            door_open = state; 
        }

        //panic!!!!!!!!!!!! *kermit flapping with his arms*
        if (armed && (window_open || door_open)) {
        	//alarm = true;
            self.turnOnAlarm();
        }
    }

    msgsrv detectMotion() {
        if (armed) {
            self.turnOnAlarm();
            motion_triggered = true; 
        }
    }
    
    msgsrv turnOnAlarm() {
        alarm = true; 
    }
    
    msgsrv turnOffAlarm() {
        alarm = false; 
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Controller controller; 
    }

    statevars { 
        boolean isOpen; 
        int sensor_id;     
    }

    Sensor(int id){
        sensor_id = id; 
        isOpen = false; 
    }

    msgsrv opened() {
        isOpen = true;
        // self.notifyController();
        controller.setSensorState(sensor_id, true);
        //self.setState(true); // after(1);
    }

    msgsrv closed() {
        isOpen = false;
        // self.notifyController();
        //self.setState(false);
        controller.setSensorState(sensor_id, false);
    }

    msgsrv setState(boolean state) {
        isOpen = state;
    }

    // msgsrv notifyController() {
    //     controller.setSensorState(sensor_id, isOpen);
    // }

    // msgsrv reset() {
    //     isOpen = false;
    // }
}

reactiveclass MotionSensor(5) {
    knownrebecs { 
        Controller controller; 
    }

    statevars { 
        boolean isTriggered; 
    }

    msgsrv setTriggered() {
        isTriggered = true;
        controller.detectMotion();
    }

    msgsrv reset() {
        isTriggered = false;
    }
}

reactiveclass Authentication(10) {
    knownrebecs{
        Controller controller;
    }

    statevars {
        boolean auth_key;
        int valid_key;
        int valid_code;
        int attempts;
    }

    Authentication(int key, int code) {
        valid_key = key;
        valid_code = code; 
     	attempts = 0;   
       	auth_key = false;
    }

    msgsrv checkKey(int key) {
        // delay(5);
		attempts += 1;

        if (key == valid_key && attempts < allowed_attempts) {
            auth_key = true; 
        } else {
            auth_key = false;
        }
    }

    msgsrv checkCode(int code) {   
        if (auth_key) {
	        controller.authenticatedUser();	        
	        self.resetAttempts();
        }
    }
    
    msgsrv resetAttempts() {
    	auth_key = false;
        attempts = 0;
    }
	
    msgsrv authenticate(int code) {
        attempts += 1;

        if (attempts > allowed_attempts) {
            auth_key = false;
        }

        if (auth_key) {
            if (code == valid_code) {
                controller.authenticatedUser();
                auth_key = false;
                attempts = 0;
            } 
        }

    }
    
}

reactiveclass Door(5) {
    knownrebecs {
        Controller controller;
    }
    statevars {
         boolean isLocked;
    }
    Door() {
        isLocked = false;
    }

    msgsrv setDoorlocked () {
        delay(25);
        isLocked = true;
    }
    
    msgsrv setDoorUnlocked () {
        delay(25);
        isLocked = false;
    }
}

reactiveclass User(5) {
    knownrebecs { 
        Sensor window_sensor, door_sensor;
        MotionSensor motion_sensor; 
        Authentication authentication;
    }
    statevars {
        int _key, _code;
    }

    User(int key, int code) {
        _key = key;
        _code = code;

        self.scenario() after(10);
    }

    msgsrv scenario() {
    	self.pokesLock() after(15);  // locks the door
    	self.breakWindow() after(25); 
    }

    msgsrv breakWindow() {
        window_sensor.opened();
    }

    msgsrv breakDoor() {
        door_sensor.opened();
    }   

    msgsrv breakDances() {
        motion_sensor.setTriggered();
    } 

    msgsrv pokesLock() {
        // scanna bricka
        // skriv in kod

        authentication.checkKey(user_key);
        authentication.checkCode(user_code) after(user_time_before_code);
    }
}

main {
    Controller controller(w_sensor, d_sensor, m_sensor, authentication, door):();
    Sensor w_sensor(controller):(windowSensor_id);
    Sensor d_sensor(controller):(doorSensor_id);
    MotionSensor m_sensor(controller):();
    Authentication authentication(controller):(user_key, user_code);
    Door door(controller): ();

    User user(w_sensor, d_sensor, m_sensor, authentication):(user_key, user_code);
}