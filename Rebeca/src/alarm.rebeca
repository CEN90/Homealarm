// Sensor id
env int doorSensor_id = 1;
env int windowSensor_id = 2;

// User auth
env int user_key = 3;
env int user_code = 4;
env int allowed_attempts = 5;

// Delay times
env int user_time_before_code = 2;
env int user_time_entering_code = 3;

reactiveclass Controller(10) {
    knownrebecs { 
        Sensor window_sensor, door_sensor; 
        MotionSensor motion;
        Authentication authentication;
        Door door;
    }

    statevars {
        boolean armed;
        boolean door_open;
        boolean window_open;
        boolean alarm; 
        boolean motion_triggered; 
        boolean door_locked;
    }
    
    Controller() {
        armed = false;
        door_open = false;
        window_open = false;
        alarm = false; 
        motion_triggered = false; 
        door_locked = false;
    }

    void setArmed() {
    	armed = true;
        door.setDoorlocked();
    }
    
    void setUnarmed() {
    	armed = false;
        door.setDoorUnlocked();
    }

    void turnOnAlarm() {
        alarm = true; 
    }
    
    void turnOffAlarm() {
        alarm = false; 
    }

    msgsrv authenticatedUser() {
        if (!armed) {
            //if (!door_open && !window_open){
            armed = true;
    		door.setDoorlocked();
            //} 
        } else {
            armed = false;
        	door.setDoorUnlocked();
        }
    }

    msgsrv setSensorState(int sensor, boolean state){
        if (sensor == windowSensor_id) {
            window_open = state;
        }
        
        if (sensor == doorSensor_id) {
            door_open = state; 
        }

        //panic!!!!!!!!!!!! *kermit flapping with his arms*
        if (armed && (window_open || door_open)) {
            self.turnOnAlarm();
        }
    }

    msgsrv motionsensorTriggered() {
        if (armed) {
            self.turnOnAlarm();
            motion_triggered = true; 
        }
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Controller controller; 
    }

    statevars { 
        boolean isOpen; 
        int sensor_id;     
    }

    Sensor(int id){
        sensor_id = id; 
        isOpen = false; 
    }

    msgsrv opened() {
        isOpen = true;
        controller.setSensorState(sensor_id, true);
    }

    msgsrv closed() {
        isOpen = false;
        controller.setSensorState(sensor_id, false);
    }
}

reactiveclass MotionSensor(5) {
    knownrebecs { 
        Controller controller; 
    }

    statevars { 
        boolean isTriggered; 
    }

    msgsrv setTriggered() {
        isTriggered = true;
        controller.motionsensorTriggered();
    }

    msgsrv reset() {
        isTriggered = false;
    }
}

reactiveclass Authentication(10) {
    knownrebecs{
        Controller controller;
    }

    statevars {
        boolean auth_key;
        int valid_key;
        int valid_code;
        int attempts;
    }

    Authentication(int key, int code) {
        valid_key = key;
        valid_code = code; 
     	attempts = 0;   
       	auth_key = false;
    }

    msgsrv checkKey(int key) {
		attempts += 1;

        if (key == valid_key && attempts < allowed_attempts) {
            auth_key = true; 
        } else {
            auth_key = false;
        }
    }

    msgsrv checkCode(int code) {   
        if (auth_key && code == valid_code) {
	        controller.authenticatedUser();	   
            auth_key = false;
            attempts = 0;   
        }
    }
}

reactiveclass Door(5) {
    knownrebecs {
        Sensor d_sensor;
    }

    statevars {
        boolean isLocked;
        boolean isOpen;
    }

    Door() {
        isLocked = false;
        isOpen = false;
    }

    msgsrv setDoorOpen() {
        if (!isLocked && !isOpen) {
            delay(5);
            d_sensor.opened();
            isOpen = true;
        }
    }

    msgsrv setDoorClosed() {
        if (isOpen) {
            delay(5);
            d_sensor.closed();
            isOpen = false;
        }
    }

    msgsrv setDoorlocked () {
        if (isOpen) {
            delay(5);
            isLocked = true;
        }
    }
    
    msgsrv setDoorUnlocked () {
        delay(5);
        isLocked = false;
    }
}

reactiveclass User(5) {
    knownrebecs { 
        Sensor window_sensor, door_sensor;
        MotionSensor motion_sensor; 
        Authentication authentication;
        Door door;
    }
    statevars {
        int _key, _code;
        int attempts;
    }

    User(int key, int code) {
        _key = key;
        _code = code;
        attempts = 0;

        self.scenario();
    }

    msgsrv scenario() {
        delay(120);

    	// self.pokesLock();  // locks the door
        authentication.checkKey(user_key);
        delay(user_time_before_code);
        authentication.checkCode(user_code);
    	
    	delay(60); // Buying milk

        // self.pokesLock(); // Back from ICA
        authentication.checkKey(user_key);
        delay(user_time_before_code);
        authentication.checkCode(user_code);

        door.setDoorOpen();
        delay(10); // walks through
        door.setDoorClosed();
        
        self.scenario(); // Goes to sleep   		
    }
    
    msgsrv breakWindow() {
        window_sensor.opened();
    }

    msgsrv breakDoor() {
        door_sensor.opened();
    }   

    msgsrv breakDances() {
        motion_sensor.setTriggered();
    } 

    msgsrv pokesLock() {
        authentication.checkKey(user_key);
        delay(user_time_before_code);
        authentication.checkCode(user_code);
        // authentication.checkCode(?(13, user_code));
    }
}

main {
    Controller controller(w_sensor, d_sensor, m_sensor, authentication, door):();
    Sensor w_sensor(controller):(windowSensor_id);
    Sensor d_sensor(controller):(doorSensor_id);
    MotionSensor m_sensor(controller):();
    Authentication authentication(controller):(user_key, user_code);
    Door door(d_sensor): ();

    User user(w_sensor, d_sensor, m_sensor, authentication, door):(user_key, user_code);
}